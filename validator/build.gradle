apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

repositories {
    maven { url 'https://raw.githubusercontent.com/webarata3/maven/master/repository' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'link.webarata3.dro.common:dro-common-util7:0.2.4'

    testCompile 'junit:junit:4.12'
    // Android JUnit Runner
    androidTestCompile 'com.android.support.test:runner:0.5'
    // JUnit4 Rules (使わなければ不要)
    androidTestCompile 'com.android.support.test:rules:0.5'
    // 古い書き方 androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
}

apply plugin: "maven"

def repo = new File(rootDir, "build/repository")

uploadArchives {
    repositories {
        mavenDeployer {
            repository url: "file://${repo.absolutePath}"
            pom.version = '0.1.3'
            pom.groupId = 'link.arata.dro.common'
            pom.artifactId = 'dro-validator'
        }
    }
}

// http://vividcode.hatenablog.com/entry/howto/publish-aar-package-to-maven-central-with-gradle
android.libraryVariants.all { variant ->
    if (variant.name == 'release') {
        task("sourcesJar${variant.name.capitalize()}", type: Jar) {
            classifier = "sources"
            from variant.javaCompile.source
        }

        artifacts {
            archives tasks["sourcesJar${variant.name.capitalize()}"]
        }
    }
}

apply plugin: 'com.github.kt3k.coveralls'

coveralls {
    jacocoReportPath = 'build/reports/coverage/debug/report.xml'
}
