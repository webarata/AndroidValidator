apply plugin: 'com.android.library'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
}

repositories {
    maven { url 'https://raw.githubusercontent.com/webarata/maven/master/repository' }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile 'com.android.support.test:testing-support-lib:0.1'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'link.arata.common:common-util7:0.1.4'
}

apply plugin: "maven"

def repo = new File(rootDir, "build/repository")

uploadArchives {
    repositories {
        mavenDeployer {
            repository url: "file://${repo.absolutePath}"
            pom.version = '0.1.2'
            pom.groupId = 'link.arata.dro.common'
            pom.artifactId = 'dro-validator'
        }
    }
}

// http://vividcode.hatenablog.com/entry/howto/publish-aar-package-to-maven-central-with-gradle
android.libraryVariants.all { variant ->
    if (variant.name == 'release') {
        task("sourcesJar${variant.name.capitalize()}", type: Jar) {
            classifier = "sources"
            from variant.javaCompile.source
        }

        artifacts {
            archives tasks["sourcesJar${variant.name.capitalize()}"]
        }
    }
}

apply plugin: 'com.github.kt3k.coveralls'

coveralls {
    jacocoReportPath = 'build/reports/coverage/debug/report.xml'
}
